#!/usr/bin/perl

use strict;
use warnings;

use lib qw(/usr/share/slackman/lib/perl5);

use Net::DBus;

my $action  = shift;
my $package = shift;
my @actions = ( 'install', 'remove', 'upgrade' );

unless ($action && $package) {
  print "Usage: $0 [install|upgrade|remove] PACKAGE\n\n";
  print "SlackMan sample D-Bus/PolicyKit wrapper for Slackware pkgtools\n\n";
  exit(1);
}

unless ( grep { $action eq $_ } @actions ) {
  print "Unknown action\n";
  exit(1);
}

if ($action ne 'remove') {

  unless ( -f $package ) {
    print "Package '$package' not found: $!\n";
    exit(1);
  }

}

my $bus = Net::DBus->system;

# Set D-Bus timeout to 5min for big packages (eg. libreoffice, kernel-source, etc.)
$bus->timeout(5 * 60 * 1000);

# Connect to SlackMan D-Bus interface
my $slackman = $bus->get_service('org.lotarproject.SlackMan')
                   ->get_object('/org/lotarproject/SlackMan');

my $pkg_info = $slackman->PackageInfo($package);

if ( $action eq 'remove' || $action eq 'upgrade' ) {
  unless ( $pkg_info->{'name'} ) {
    print "Package $package not installed!\n";
    exit(1);
  }
}

if ( $action eq 'install' && $pkg_info->{'name'} ) {
  print "Package $package already installed!\n";
  exit(1);
}

my $retval = undef;

$retval = $slackman->InstallPkg($package) if ($action eq 'install');
$retval = $slackman->UpgradePkg($package) if ($action eq 'upgrade');
$retval = $slackman->RemovePkg($package)  if ($action eq 'remove');

if ($retval) {
  print "Problem during execution of program (exit: $retval)\n";
} else {
  print "done (exit: $retval)\n";
}

exit($retval);
